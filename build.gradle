// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.1.4'
    ext.support_library_version = '26.0.2'
    ext.mockito_version = '2.8.47'
    ext.truth_version = '0.34'
    ext.junit_params_version = '1.1.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()

        maven {
            url "https://maven.google.com"
        }
    }

    tasks.matching {it instanceof Test}.all {
        testLogging.events = ["failed", "passed", "skipped"]
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

/**
 * Looks for the specified configuration key in this order:
 *
 * <ol>
 *     <li>system environment</li>
 *     <li>project properties</li>
 * </ol>
 *
 * If it isn't found, then the empty string is returned. This stops the gradlew from failing until
 * it actually needs those values.
 *
 * @param keyName The key to look for
 * @return The value or the empty string if not found
 */
def getConfigurationParameter(String keyName) {
    return System.getenv(keyName) ?:
            rootProject.hasProperty(keyName) ? rootProject.properties[keyName] : ''
}
